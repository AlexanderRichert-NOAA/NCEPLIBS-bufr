!> @file
!> @brief Check whether a BUFR message contains DX BUFR tables information

!> This function determines whether a given BUFR message contains DX BUFR tables information that was generated by the
!> BUFRLIB software.
!>
!> @author J. Ator
!> @date 2009-03-23
!>
!> @param[in] MESG   -- integer(*): BUFR message
!> @returns idxmsg   -- integer: Flag indicating whether MESG contains DX BUFR tables information:
!>                        - 0 = No
!>                        - 1 = Yes
!>
!> <b>Program history log:</b>
!> | Date | Programmer | Comments |
!> | -----|------------|----------|
!> | 2009-03-23 | J. Ator | Original author |
!> | 2022-02-01 | J. Ator | Converted to module to consolidate _4, _d, and _8 variations into one build |
!>

module function_idxmsg

    private
    public idxmsg

    interface idxmsg
        module procedure idxmsg_4_d, idxmsg_8
    end interface

    contains

    function idxmsg_4_d( mesg )
!       used when call arguments to idxmsg are 4-byte integers

        implicit none

        integer(kind=4), intent(in) :: mesg(:)
        integer(kind=4) :: idxmsg_4_d

        integer :: my_mesg(size(mesg))

        my_mesg(1:size(mesg)) = mesg(1:size(mesg))

        idxmsg_4_d = idxmsg_body( my_mesg )

    end function idxmsg_4_d

    function idxmsg_8( mesg )
!       used when call arguments to idxmsg are 8-byte integers

        implicit none

        integer(kind=8), intent(in) :: mesg(:)
        integer(kind=8) :: idxmsg_8

        integer :: my_mesg(size(mesg))

        my_mesg(1:size(mesg)) = mesg(1:size(mesg))

        idxmsg_8 = idxmsg_body( my_mesg )

    end function idxmsg_8

    function idxmsg_body( MESG )
        
        use function_iupbs01

        integer, intent(in) :: MESG(:)

!-----------------------------------------------------------------------
!-----------------------------------------------------------------------

!	Note that the following test relies upon logic within subroutine DXMINI which zeroes out the Section 1 date
!	of all DX dictionary messages.

	IF ( (IUPBS01(MESG,'MTYP').EQ.11) .AND. &
             (IUPBS01(MESG,'MNTH').EQ.0) .AND. (IUPBS01(MESG,'DAYS').EQ.0) .AND. (IUPBS01(MESG,'HOUR').EQ.0) ) THEN
	   IDXMSG_BODY = 1
	ELSE
	   IDXMSG_BODY = 0
	END IF

	RETURN

    end function idxmsg_body

end
